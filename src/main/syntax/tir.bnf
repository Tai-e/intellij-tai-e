{
  parserClass="pascal.taie.intellij.tir.syntax.TirParser"

  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Tir"
  psiImplClassSuffix="Impl"
  psiPackage="pascal.taie.intellij.tir.syntax.psi"
  psiImplPackage="pascal.taie.intellij.tir.syntax.psi.impl"

  elementTypeHolderClass="pascal.taie.intellij.tir.syntax.TirTypes"
  elementTypeClass="pascal.taie.intellij.tir.syntax.TirElementType"
  tokenTypeClass="pascal.taie.intellij.tir.syntax.TirTokenType"

  psiImplUtilClass="pascal.taie.intellij.tir.syntax.TirPsiUtil"
}

tir_file ::= class_def

class_def ::= annotation_def* modifier_class* (CLASS | INTERFACE) identifier_class class_extends? class_implements? class_body

class_extends ::= EXTENDS identifier_class (COMMA identifier_class)*

class_implements ::= IMPLEMENTS identifier_class (COMMA identifier_class)*

class_body ::= LBRACE class_member* RBRACE

class_member ::= field_def | method_def | COMMENT

// Annotation
annotation_def ::= annotation_def_name (LPAREN (annotation_key_element_list | annotation_element)? RPAREN)?

annotation_def_name ::= AT identifier_class

annotation_key_element_list ::= annotation_key_element (COMMA annotation_key_element)*

annotation_key_element ::= IDENTIFIER EQUAL annotation_element

annotation_element ::= floating_point_annotation_element
                     | identifier_class
                     | integer_annotation_element
                     | boolean_annotation_element
                     | class_annotation_element
                     | array_annotation_element
                     | string_annotation_element
                     | annotation_def

floating_point_annotation_element ::= FLOAT | DOUBLE

integer_annotation_element ::= INTEGER | LONG

boolean_annotation_element ::= TRUE | FALSE

class_annotation_element ::= identifier_class (LBRACKET RBRACKET)* DOT CLASS

array_annotation_element ::= LBRACE annotation_element_list? RBRACE

annotation_element_list ::= annotation_element (COMMA annotation_element)*

string_annotation_element ::= STRING

// Field def
field_def ::= annotation_def* modifier_field* identifier_type identifier_field_list SEMICOLON
// ---

// Method def
method_def ::= annotation_def* modifier_method* method_sig (method_body | SEMICOLON)

method_sig ::= identifier_type identifier_method LPAREN method_sig_params? RPAREN

method_sig_params ::= annotation_def* identifier_type IDENTIFIER (COMMA annotation_def* identifier_type IDENTIFIER)*

method_body ::= LBRACE (method_body_var | method_body_line | method_body_catch)+ RBRACE

method_body_var ::= identifier_type identifier_variable (COMMA identifier_variable)* SEMICOLON

method_body_line ::= LINE_NUMBER method_body_stmt SEMICOLON

method_body_catch ::= TRY LBRACKET INTEGER COMMA INTEGER RPAREN COMMA CATCH identifier_variable CATCH_AT INTEGER

// copy stmts and more
method_body_stmt ::= lvalue EQUAL rvalue
                   | identifier_variable EQUAL NEW identifier_type
                   | identifier_variable EQUAL NEW_ARRAY identifier_type LBRACKET identifier_variable RBRACKET
                   | identifier_variable EQUAL NEW_MULTI_ARRAY identifier_type (LBRACKET identifier_variable? RBRACKET)+
                   | invoke_expr
                   | THROW identifier_type
                   | CATCH identifier_variable
                   | LOOKUP_SWITCH LPAREN identifier_variable RPAREN LBRACE switch_branch (COMMA switch_branch)* RBRACE
                   | IF LPAREN (unary_expr | binary_expr | IDENTIFIER) RPAREN GOTO INTEGER
                   | GOTO INTEGER
                   | (MONITOR_ENTER | MONITOR_EXIT) identifier_variable
                   | RETURN identifier_variable?
                   | NOP

switch_branch ::= (INTEGER | LONG | STRING | DEFAULT) RIGHT_ARROW INTEGER

lvalue ::= identifier_variable DOT field_sig
         | identifier_variable LBRACKET identifier_variable RBRACKET
         | identifier_variable
         | field_sig  // Static field

rvalue ::= reference_literal
         | number_literal
         | identifier_variable DOT field_sig  // Instance field
         | identifier_variable LBRACKET identifier_variable RBRACKET  // Array index
         | field_sig  // Static field
         | LPAREN identifier_type RPAREN identifier_variable  // Cast
         | invoke_expr
         | unary_expr
         | binary_expr
         | identifier_variable INSTANCEOF identifier_type
         | identifier_variable

field_sig ::= LANGLE field_ref RANGLE

field_ref ::= IDENTIFIER COLON identifier_type IDENTIFIER

unary_expr ::= (ADD_OP | SUB_OP | NOT_OP) identifier_variable

binary_expr ::= identifier_variable (LANGLE | RANGLE | CMP_OP | BIT_OP | ADD_OP | SUB_OP | MUL_OP | DIV_OP | MOD_OP) identifier_variable

// literal
number_literal ::= integer_literal | floating_point_literal

integer_literal ::= INTEGER | LONG

floating_point_literal ::= FLOAT | DOUBLE

reference_literal ::= string_literal | class_literal | null_literal | method_type_literal | method_handle_literal

string_literal ::= STRING

null_literal ::= NULL

class_literal ::= identifier_class (LBRACKET RBRACKET)* DOT CLASS

method_type_literal ::= METHOD_TYPE COLON identifier_type LPAREN invoke_argument_type_list? RPAREN

// TODO
method_handle_literal ::= METHOD_HANDLE LBRACKET identifier_variable RBRACKET COLON (field_ref | invoke_method_ref)

// invoke
invoke_expr ::= invoke_instance_expr | invoke_static_expr | invoke_dynamic_expr

invoke_instance_key ::= INVOKE_SPECIAL | INVOKE_VIRTUAL | INVOKE_INTERFACE

invoke_instance_expr ::= invoke_instance_key identifier_variable DOT invoke_method_ref LPAREN invoke_argument_list? RPAREN

invoke_method_ref ::= LANGLE identifier_class COLON identifier_type identifier_method LPAREN invoke_argument_type_list? RPAREN RANGLE

invoke_static_expr ::= INVOKE_STATIC invoke_method_ref LPAREN invoke_argument_list? RPAREN

// invoke dynamic
invoke_dynamic_expr ::= INVOKE_DYNAMIC invoke_method_ref string_literal invoke_method_type LBRACKET invoke_bootstrap_argument_list? RBRACKET LPAREN invoke_argument_list? RPAREN

invoke_method_type ::= LANGLE method_type_literal RANGLE

invoke_bootstrap_argument_list ::= reference_literal (COMMA reference_literal)*

invoke_argument_type_list ::= identifier_type (COMMA identifier_type)*

invoke_argument_list ::= identifier_variable (COMMA identifier_variable)*
// ---

// modifiers
// https://docs.oracle.com/javase/specs/jls/se17/html/jls-8.html#jls-8.1.1
modifier_class ::= PUBLIC | PROTECTED | PRIVATE | ABSTRACT | STATIC | FINAL | SEALED | NON_SEALED | STRICTFP | SYNTHETIC | ENUM | ANNOTATION

// https://docs.oracle.com/javase/specs/jls/se17/html/jls-8.html#jls-8.3.1
modifier_field ::= PUBLIC | PROTECTED | PRIVATE | STATIC | FINAL | TRANSIENT | VOLATILE | SYNTHETIC | ENUM

// https://docs.oracle.com/javase/specs/jls/se17/html/jls-8.html#jls-8.4.3
modifier_method ::= PUBLIC | PROTECTED | PRIVATE | ABSTRACT | STATIC | FINAL | SYNCHRONIZED | NATIVE | STRICTFP | SYNTHETIC
// ---

// helpers
identifier_class ::= IDENTIFIER

identifier_field ::= IDENTIFIER

identifier_method ::= IDENTIFIER | LANGLE IDENTIFIER RANGLE

identifier_variable ::= IDENTIFIER | CONSTANT_IDENTIFIER  {
    mixin="pascal.taie.intellij.tir.syntax.TirNamedElementMixin"
    implements="pascal.taie.intellij.tir.syntax.TirNamedElement"
}

identifier_type ::= identifier_class (LBRACKET RBRACKET)* | NULL_TYPE

identifier_field_list ::= identifier_field (COMMA identifier_field)*
